Option Explicit

' --- VARIABLEN UMBENANNT ZUR VERMEIDUNG VON KONFLIKTEN ---
Public arrDocID As Variant
Public arrTechStart As Variant
Public arrTechEnd As Variant
Public arrOrigStart As Variant
Public arrOrigEnd As Variant
Public arrOrigText As Variant
Public arrDatum As Variant  ' Hieß vorher Datum (Konfliktpotenzial!)
Public arrVorsitzFkt As Variant
Public arrVorsitzVN As Variant
Public arrVorsitzNN As Variant
Public arrVorsitzHLS As Variant
Public arrVorsitzGND As Variant
Public arrGremium As Variant ' Hieß vorher Gremium (evtl. Modulname?)

Sub Start()
    Dim FSO, ExcelDateien, exceldatei
    Dim Pfad As String
    Dim MetadatenOrdner
    Pfad = "I:\06_Abteilung_Editionsprojekte\Produktive_Daten_Projekt_PVQ\Ratsmanuale_B_II_6-1060\03_Metadaten_und_QS\Metadaten_4_Sitzungen_Ablage"
    Set FSO = CreateObject("Scripting.FileSystemObject")
    Set MetadatenOrdner = FSO.GetFolder(Pfad)
    Set ExcelDateien = MetadatenOrdner.Files
    Dim exceldoc As Workbook
    Dim Wb As Workbook
    Set Wb = Workbooks.Add
    Wb.Title = "Sitzungsmetadaten Ratsmanuale"
    Dim cellcounter As Double
    cellcounter = 2
    Dim Datsheet_Result As Worksheet
    Set Datsheet_Result = Wb.Worksheets(1)
    Datsheet_Result.Application.Visible = True
     
    Dim DatArray() As String
    Dim Jahr As Integer
    Dim Monat As Integer
    Dim Tag As Integer
    Dim Wochentag As String

    Dim JDatum
    Dim JM As Integer
    Dim JY As Integer
    Dim JD As Integer

    Dim d As Integer
    Dim m As Integer
    Dim y As Integer
    Dim y1 As Integer
    Dim c As Integer
    Dim c1 As Integer
    Dim w As Integer
    Dim julianischesDatum As Boolean
    Dim LastRow As Long
     
    For Each exceldatei In ExcelDateien
        If Not (exceldatei.Name Like "*Metadaten.xls*") Then
            GoTo NextExcel
        Else
        Set exceldoc = Workbooks.Open(Pfad & "\" & exceldatei.Name)
         
        Dim Datsheet As Worksheet
        Set Datsheet = exceldoc.Worksheets(1)
        
        ' Letzte Zeile sicher ermitteln
        LastRow = Datsheet.Cells(Datsheet.Rows.Count, 1).End(xlUp).Row
        
        ' Sicherheitscheck: Wenn Datei leer, überspringen
        If LastRow < 2 Then
            exceldoc.Close SaveChanges:=False
            GoTo NextExcel
        End If
         
        ' Daten in die umbenannten Arrays einlesen
        arrDocID = Datsheet.Range("A2:A" & LastRow).Value
        arrTechStart = Datsheet.Range("B2:B" & LastRow).Value
        arrTechEnd = Datsheet.Range("C2:C" & LastRow).Value
        arrOrigStart = Datsheet.Range("D2:D" & LastRow).Value
        arrOrigEnd = Datsheet.Range("E2:E" & LastRow).Value
        arrOrigText = Datsheet.Range("F2:F" & LastRow).Value
        arrDatum = Datsheet.Range("G2:G" & LastRow).Value
        arrVorsitzFkt = Datsheet.Range("H2:H" & LastRow).Value
        arrVorsitzVN = Datsheet.Range("I2:I" & LastRow).Value
        arrVorsitzNN = Datsheet.Range("J2:J" & LastRow).Value
        arrVorsitzHLS = Datsheet.Range("K2:K" & LastRow).Value
        arrVorsitzGND = Datsheet.Range("L2:L" & LastRow).Value
        arrGremium = Datsheet.Range("M2:M" & LastRow).Value

        Datsheet_Result.Range("A1").Value = "Band"
        Datsheet_Result.Range("B1").Value = "technische_Startseite"
        Datsheet_Result.Range("C1").Value = "technische_Endseite"
        Datsheet_Result.Range("D1").Value = "original_Startseite"
        Datsheet_Result.Range("E1").Value = "original_Endseite"
        Datsheet_Result.Range("F1").Value = "Seiten_Zusatz"
        Datsheet_Result.Range("G1").Value = "original_text"
        Datsheet_Result.Range("H1").Value = "Datum"
        Datsheet_Result.Range("I1").Value = "Vorsitz_Funktion"
        Datsheet_Result.Range("J1").Value = "Vorsitz_Vorname"
        Datsheet_Result.Range("K1").Value = "Vorsitz_Nachname"
        Datsheet_Result.Range("L1").Value = "Vorsitz_HLS"
        Datsheet_Result.Range("M1").Value = "Vorsitz_GND"
        Datsheet_Result.Range("N1").Value = "Gremium"
        Datsheet_Result.Range("O1").Value = "Wochentag"
        Datsheet_Result.Range("P1").Value = "Doc-Id"
        Datsheet_Result.Range("Q1").Value = "Weblink"
         
        Dim BandSign As String
        BandSign = Replace(exceldatei.Name, "_Metadaten.xlsx", "")

        Dim k As Double
        ' Schleife läuft nun über das umbenannte Array
        For k = 1 To UBound(arrDocID)

        Datsheet_Result.Range("A" & cellcounter).Value = BandSign
        Datsheet_Result.Range("B" & cellcounter).Value = arrTechStart(k, 1)
        Datsheet_Result.Range("C" & cellcounter).Value = arrTechEnd(k, 1)
        Datsheet_Result.Range("D" & cellcounter).Value = arrOrigStart(k, 1)
        Datsheet_Result.Range("E" & cellcounter).Value = arrOrigEnd(k, 1)
         
        If k > 1 Then
            If arrOrigStart(k - 1, 1) = arrOrigStart(k, 1) And arrOrigEnd(k - 1, 1) = arrOrigEnd(k, 1) Then
                Datsheet_Result.Range("D" & cellcounter).Interior.ColorIndex = 6
                Datsheet_Result.Range("E" & cellcounter).Interior.ColorIndex = 6
                Datsheet_Result.Range("F" & cellcounter).Interior.ColorIndex = 6
                Datsheet_Result.Range("D" & cellcounter - 1).Interior.ColorIndex = 6
                Datsheet_Result.Range("E" & cellcounter - 1).Interior.ColorIndex = 6
                Datsheet_Result.Range("F" & cellcounter - 1).Interior.ColorIndex = 6
                Datsheet_Result.Range("F" & cellcounter).Value = ", Eintrag 2"
                Datsheet_Result.Range("F" & cellcounter - 1).Value = ", Eintrag 1"
            End If
        End If
        Datsheet_Result.Range("G" & cellcounter).Value = arrOrigText(k, 1)
         
        If InStr(LCase(arrOrigText(k, 1)), "sekel") > 0 Or InStr(LCase(arrOrigText(k, 1)), "seck") > 0 Then
            Datsheet_Result.Range("G" & cellcounter).Interior.ColorIndex = 28
        End If
         
        Datsheet_Result.Range("H" & cellcounter).Value = arrDatum(k, 1)
         
        If arrDatum(k, 1) = "" Then
            Datsheet_Result.Range("H" & cellcounter).Interior.ColorIndex = 22
            ' Datum autom. befüllen (gefordert)
            Datsheet_Result.Range("H" & cellcounter).Value = arrDatum(k - 1, 1)
        End If
         
        ' --- KEINE AUTOMATIK MEHR FÜR I BIS N ---
        Datsheet_Result.Range("I" & cellcounter).Value = arrVorsitzFkt(k, 1)
        If Datsheet_Result.Range("I" & cellcounter).Value <> "Bürgermeister" And Datsheet_Result.Range("I" & cellcounter).Value <> "" Then
            Datsheet_Result.Range("I" & cellcounter).Interior.ColorIndex = 28
        End If
         
        Datsheet_Result.Range("J" & cellcounter).Value = arrVorsitzVN(k, 1)
        Datsheet_Result.Range("K" & cellcounter).Value = arrVorsitzNN(k, 1)
        Datsheet_Result.Range("L" & cellcounter).Value = arrVorsitzHLS(k, 1)
        Datsheet_Result.Range("M" & cellcounter).Value = arrVorsitzGND(k, 1)
        Datsheet_Result.Range("N" & cellcounter).Value = arrGremium(k, 1)
        ' ----------------------------------------
        
        Datsheet_Result.Range("P" & cellcounter).Value = arrDocID(k, 1)
         
        Datsheet_Result.Range("Q" & cellcounter).Value = "https://app.transkribus.org/deu/sites/zuercher-ratsmanuale/doc/" & arrDocID(k, 1) & "/detail?pageid=" & arrTechStart(k, 1)
         
        ' Datum Logik nutzt jetzt arrDatum
        If arrDatum(k, 1) <> "" Then
            DatArray = Split(arrDatum(k, 1), ".")
            Jahr = CInt(DatArray(0))
            Monat = CInt(DatArray(1))
            Tag = CInt(DatArray(2))

            If Jahr <= 1700 Then
            julianischesDatum = True
            Else
            julianischesDatum = False
            End If

            If julianischesDatum Then
            JD = Tag
            If Monat > 2 Then
                JY = Jahr
                JM = Monat
            Else
                JY = Jahr - 1
                JM = Monat + 12
            End If
            JDatum = Fix(365.25 * (JY + 4716)) + Fix(30.6001 * (JM + 1)) + JD + 0.5 - 1524.5
            w = (JDatum + 1) Mod 7
            Else
            d = Tag
            m = getM(Monat)

            If Monat = 1 Or Monat = 2 Then
                y1 = CInt(Right(DatArray(0), 2))
                If y1 = 0 Then
                    y = 99
                Else
                    y = y1 - 1
                End If
            Else
                y = CInt(Right(DatArray(0), 2))
            End If

            If Monat = 1 Or Monat = 2 Then
                c1 = CInt(Left(DatArray(0), 2))
                If y1 = 0 Then
                    c = c1 - 1
                Else
                    c = c1
                End If
            Else
                c = CInt(Left(DatArray(0), 2))
            End If
            w = (d + Fix(2.6 * m - 0.2) + y + Fix(y / 4) + Fix(c / 4) - (2 * c)) Mod 7
            If w < 0 Then
                w = w + 7
            End If
            End If

            Wochentag = getDay(w)
            Datsheet_Result.Range("O" & cellcounter).Value = Wochentag
        Else
            ' Korrektur: Kopiere von O (Wochentag) darüber, nicht N
            Datsheet_Result.Range("O" & cellcounter).Interior.ColorIndex = 22
            Datsheet_Result.Range("O" & cellcounter).Value = Datsheet_Result.Range("O" & cellcounter - 1).Value
        End If
         
        cellcounter = cellcounter + 1
        Next k
        End If
     
    exceldoc.Close SaveChanges:=False
         
NextExcel:
    Next
    Datsheet_Result.Name = "Masterdaten"
    Dim Datsheet_Scope As Worksheet
    Set Datsheet_Scope = Wb.Worksheets.Add
    Datsheet_Scope.Name = "Scope-Import"

    Datsheet_Scope.Range("A1").Value = "Stufe"
    Datsheet_Scope.Range("B1").Value = "Signatur"
    Datsheet_Scope.Range("C1").Value = "Titel"
    Datsheet_Scope.Range("D1").Value = "Inhalt und Form"
    Datsheet_Scope.Range("E1").Value = "Entstehungszeitraum"
    Datsheet_Scope.Range("F1").Value = "Entstehungszeitraum, Anm."
    Datsheet_Scope.Range("G1").Value = "Weblinks"

    Datsheet_Scope.Range("A2:A" & cellcounter - 1) = "=""Subdossier"""
    Datsheet_Scope.Range("B2:B" & cellcounter - 1) = "=CONCATENATE(SUBSTITUTE(Masterdaten!RC[-1],""_"","" ""),"" (S. "",IF(Masterdaten!RC[2]=Masterdaten!RC[3],CONCATENATE(Masterdaten!RC[3],Masterdaten!RC[4]),CONCATENATE(Masterdaten!RC[2],""-"",Masterdaten!RC[3])),"")"")"
    
    Datsheet_Scope.Range("C2:C" & cellcounter - 1).FormulaR1C1 = _
        "=IF(Masterdaten!RC[8]=""""," & _
        "CONCATENATE(""Ratssitzung ("",Masterdaten!RC[11],"")"")," & _
        "CONCATENATE(""Ratssitzung ("",Masterdaten!RC[11],"") unter dem Vorsitz von "",Masterdaten!RC[6],"" "",Masterdaten!RC[7],"" "",Masterdaten!RC[8]))"
    
    Datsheet_Scope.Range("D2:D" & cellcounter - 1) = "=CONCATENATE(""Originaltitel: "",Masterdaten!RC[3])"
    Datsheet_Scope.Range("E2:E" & cellcounter - 1) = "=CONCATENATE(""x= +"",LEFT(Masterdaten!RC[3],4),RIGHT(LEFT(Masterdaten!RC[3],7),2),RIGHT(Masterdaten!RC[3],2))"
    Datsheet_Scope.Range("F2:F" & cellcounter - 1) = "=Masterdaten!RC[9]"
    Datsheet_Scope.Range("G2:G" & cellcounter - 1) = "=Masterdaten!RC[10]"

    MsgBox "Ende erreicht!"
    Wb.SaveAs filename:=Pfad & "\" & "Sitzungsmetadaten_zusammengefügt.xlsx"
End Sub

Public Function getDay(TagAlsZahl As Integer) As String
Select Case TagAlsZahl
    Case 1
        getDay = "Montag"
    Case 2
        getDay = "Dienstag"
    Case 3
        getDay = "Mittwoch"
    Case 4
        getDay = "Donnerstag"
    Case 5
        getDay = "Freitag"
    Case 6
        getDay = "Samstag"
    Case 0
        getDay = "Sonntag"
    Case Else
        getDay = "Da stimmt etwas nicht"
End Select
End Function

Public Function getM(Monat As Integer) As Integer
Select Case Monat
    Case 1
        getM = 11
    Case 2
        getM = 12
    Case 3
        getM = 1
    Case 4
        getM = 2
    Case 5
        getM = 3
    Case 6
        getM = 4
    Case 7
        getM = 5
    Case 8
        getM = 6
    Case 9
        getM = 7
    Case 10
        getM = 8
    Case 11
        getM = 9
    Case 12
        getM = 10
    Case Else
        getM = 0
End Select
End Function

